package com.suse.matcher.rules.drools;

import com.suse.matcher.Drools;
import com.suse.matcher.facts.HostedProduct;
import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.InstalledProduct;
import com.suse.matcher.facts.PenaltyGroup;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.SubscriptionProduct;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.VirtualizationGroupMember;

rule "generateHostedProduct"
    agenda-group "InputAugmenting"
    when
        System($systemId : id, physical == false)
        InstalledProduct(systemId == $systemId, $productId : productId)
        HostGuest($hostId : hostId, guestId == $systemId)
    then
        insert(new HostedProduct($hostId, $productId));
end

rule "generatePenaltyGroupsFromVirtualizationGroups"
    agenda-group "InputAugmenting"
    when
        VirtualizationGroupMember($virtualizationGroupId : virtualizationGroupId, $guestId : guestId)
    then
        insert(new PenaltyGroup($virtualizationGroupId, $guestId));
end

rule "generatePenaltyGroups"
    agenda-group "InputAugmenting"
    when
        HostGuest($hostId : hostId, $guestId : guestId)
        not VirtualizationGroupMember(guestId == $guestId)
    then
        int penaltyGroupId = Drools.generateId("host_guest", $hostId);
        insert(new PenaltyGroup(penaltyGroupId, $guestId));
end

rule "markHardBundledSubscriptions"
    agenda-group "InputAugmenting"
    when
        // two subscriptions with different product sets, but otherwise identical, are by definition hard bundled
        $s1 : Subscription(ignored == false)
        $s2 : Subscription(ignored == false, partNumber == $s1.partNumber, id > $s1.id, startDate == $s1.startDate, endDate == $s1.endDate, quantity == $s1.quantity)
        (
            SubscriptionProduct($productId : productId, subscriptionId == $s1.id) and
            not SubscriptionProduct(productId == $productId, subscriptionId == $s2.id)
        ) or
        (
            SubscriptionProduct($productId : productId, subscriptionId == $s2.id) and
            not SubscriptionProduct(productId == $productId, subscriptionId == $s1.id)
        )
    then
        int hardBundleId = Drools.generateId($s1.partNumber, $s1.startDate, $s1.endDate, $s1.quantity);
        modify($s1) {
            hardBundleId = hardBundleId;
        }
        modify($s2) {
            hardBundleId = hardBundleId;
        }
end
