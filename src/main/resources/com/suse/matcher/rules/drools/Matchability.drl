package com.suse.matcher.rules.drools;

import java.util.List;

import com.suse.matcher.facts.FreeMatch;
import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.PossibleMatch;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.Subscription.Policy;
import com.suse.matcher.facts.SubscriptionProduct;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.SystemProduct;

rule "matchPhysicalOnlySubscription"
    when
        $system : System($systemId : id, physical == true)
        SystemProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.PHYSICAL_ONLY,
            cpus >= $system.cpus,
            ignored == false
        )
    then
        insertLogical(new PossibleMatch($system.id, $productId, $subscription.id, 100));
end

rule "matchInstanceSubscription"
    when
        $system : System($systemId : id)
        SystemProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.INSTANCE,
            (cpus == null || cpus >= $system.cpus),
            ignored == false
        )
    then
        insertLogical(new PossibleMatch($system.id, $productId, $subscription.id, 100));
end

rule "matchUnlimitedVirtualizationSubscription"
    when
        $system : System($systemId : id, physical == true, cpus != null)
        (
            SystemProduct($productId : productId, systemId == $systemId) or
            (
                HostGuest(hostId == $systemId, $guestId : guestId) and
                SystemProduct($productId : productId, systemId == $guestId)
            )
        )
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.UNLIMITED_VIRTUALIZATION,
            (cpus >= $system.cpus || stackable == true),
            ignored == false
        )
    then
        Integer consumedQuantity = ($system.cpus + $subscription.cpus - 1) / $subscription.cpus;
        insertLogical(new PossibleMatch($system.id, $productId, $subscription.id, consumedQuantity * 100));
end

rule "matchFreeVirtualizationSubscription"
    when
        $match : PossibleMatch($hostId : systemId, $productId : productId, $subscriptionId : subscriptionId)
        Subscription(id == $subscriptionId, policy == Policy.UNLIMITED_VIRTUALIZATION, ignored == false)
        HostGuest(hostId == $hostId, $guestId : guestId)
    then
        insertLogical(new FreeMatch($guestId, $productId, $subscriptionId, $match.id));
end

rule "match22Subscription"
    when
        $system : System($systemId : id, cpus != null)
        SystemProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.TWO_TWO,
            ignored == false
        )
        not (HostGuest(hostId == $systemId))
    then
        if ($system.physical) {
            insertLogical(new PossibleMatch($system.id, $productId, $subscription.id,
                ($system.cpus + $subscription.cpus - 1) / $subscription.cpus * 100));
        }
        else {
            insertLogical(new PossibleMatch($system.id, $productId, $subscription.id, 50));
        }
end
