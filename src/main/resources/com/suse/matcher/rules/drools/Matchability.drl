package com.suse.matcher.rules.drools;

import java.util.List;

import com.suse.matcher.Drools;
import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.HostedProduct;
import com.suse.matcher.facts.IncompatibleGroups;
import com.suse.matcher.facts.InstalledProduct;
import com.suse.matcher.facts.PartialMatch;
import com.suse.matcher.facts.Product;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.Subscription.Policy;
import com.suse.matcher.facts.SubscriptionProduct;
import com.suse.matcher.facts.System;

rule "matchPhysicalOnlySubscription"
    agenda-group "Matchability"
    when
        $system : System($systemId : id, physical == true)
        InstalledProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.PHYSICAL_ONLY,
            cpus >= $system.cpus,
            ignored == false
        )
    then
        int groupId = Drools.generateId($system.id, $productId, $subscription.id);
        insert(new PartialMatch($system.id, $productId, $subscription.id, 100, groupId));
end

rule "matchInstanceSubscription"
    agenda-group "Matchability"
    when
        $system : System($systemId : id)
        InstalledProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.INSTANCE,
            (cpus == null || cpus >= $system.cpus),
            ignored == false
        )
    then
        int groupId = Drools.generateId($system.id, $productId, $subscription.id);
        insert(new PartialMatch($system.id, $productId, $subscription.id, 100, groupId));
end

rule "matchUnlimitedVirtualizationSubscription"
    agenda-group "Matchability"
    when
        $system : System($systemId : id, physical == true, cpus != null)
        (
            InstalledProduct($productId : productId, systemId == $systemId) or
            HostedProduct($productId : productId, systemId == $systemId)
        )
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.UNLIMITED_VIRTUALIZATION,
            (cpus >= $system.cpus || stackable == true),
            ignored == false
        )
    then
        Integer consumedQuantity = ($system.cpus + $subscription.cpus - 1) / $subscription.cpus;
        int groupId = Drools.generateId($system.id, $productId, $subscription.id);
        insert(new PartialMatch($system.id, $productId, $subscription.id, consumedQuantity * 100, groupId));
end

rule "matchFreeVirtualizationSubscription"
    agenda-group "Matchability"
    when
      PartialMatch($hostId : systemId, $productId : productId, $subscriptionId : subscriptionId)
      Subscription(id == $subscriptionId, policy == Policy.UNLIMITED_VIRTUALIZATION, ignored == false)
      HostGuest(hostId == $hostId, $guestId : guestId)
      InstalledProduct(systemId == $guestId, productId == $productId)
    then
      int groupId = Drools.generateId($hostId, $productId, $subscriptionId);
      insert(new PartialMatch($guestId, $productId, $subscriptionId, 0, groupId));
end

rule "matchInheritedVirtualization"
    agenda-group "Matchability"
    no-loop
    when
        // for each base product
        Product($baseProductId : id, base == true)

        // for each installed add-on product with an IV subscription
        Subscription($addonSubscriptionId : id, policy == Policy.INHERITED_VIRTUALIZATION, ignored == false)
        SubscriptionProduct($addonProductId : productId, subscriptionId == $addonSubscriptionId)
        (
            InstalledProduct($systemId : systemId, productId == $addonProductId) or
            HostedProduct($systemId : systemId, productId == $addonProductId)
        )

        // for each match
        PartialMatch($baseSubscriptionId : subscriptionId, $cents : cents, $baseGroupId : groupId, productId == $baseProductId, systemId == $systemId)
    then
        int addonGroupId = Drools.generateId($baseGroupId, $addonSubscriptionId);
        insert(new PartialMatch($systemId, $baseProductId, $baseSubscriptionId, $cents, addonGroupId));
        insert(new PartialMatch($systemId, $addonProductId, $addonSubscriptionId, $cents, addonGroupId));
end

rule "match12Subscription"
    agenda-group "Matchability"
    when
        // we want to explicitly bind 'physical' here as we want the rule to be reactivated on change of this attribute:
        $system : System($systemId : id, $physical : physical, cpus != null)
        InstalledProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.ONE_TWO,
            ignored == false
        )
        not (HostGuest(hostId == $systemId))
    then
        int groupId = Drools.generateId($systemId, $productId, $subscriptionId);
        if ($physical) {
            insert(new PartialMatch($system.id, $productId, $subscription.id,
                ($system.cpus + $subscription.cpus - 1) / $subscription.cpus * 100,
                groupId));
        }
        else {
            insert(new PartialMatch($system.id, $productId, $subscription.id, 50, groupId));
        }
end
