package com.suse.matcher.rules.optaplanner;

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;

import com.suse.matcher.facts.FreeMatch;
import com.suse.matcher.facts.PinnedMatch;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.SystemProduct;
import com.suse.matcher.solver.Match;

global HardSoftScoreHolder scoreHolder;


// Hard constraints: no solution can ever be produced if any of those is broken
// (that is, score is less than 0). If that happens, we found a bug!

// Note that the construction heuristic must be able to construct a solution
// with score 0, so that the final solution can never be unacceptable

rule "dontExceedSubscriptionCount"
    when
        Subscription($subscriptionId: id, $quantity : quantity, ignored == false)
        $requiredCents : Number(intValue > $quantity * 100) from accumulate (
            Match(subscriptionId == $subscriptionId, confirmed == true, $cents : cents),
            sum($cents)
        )
    then
        scoreHolder.addHardConstraintMatch(kcontext,
            // limit penalty to 10 points
            -10 + $quantity * 100 * 9 / $requiredCents.intValue()
        );
end

rule "dontMatchTwice"
    when
        Match($systemId : systemId, $productId : productId, $subscriptionId : subscriptionId, confirmed == true) and
        (
            Match(systemId == $systemId, productId == $productId, subscriptionId != $subscriptionId, confirmed == true) or
            (
                FreeMatch(systemId == $systemId, productId == $productId, $requiredMatchId : requiredMatchId) and
                Match(id == $requiredMatchId, confirmed == true)
            )
        )
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end

// Soft constraints: we try to find the solution that maximizes the score, but there are no guarantees.

rule "maximizeCoveredInstallations"
    when
        SystemProduct($systemId : systemId, $productId : productId) and
        (
            Match(systemId == $systemId, productId == $productId, confirmed == true) or
            (
                FreeMatch(systemId == $systemId, productId == $productId, $requiredMatchId : requiredMatchId) and
                Match(id == $requiredMatchId, confirmed == true)
            )
        )
    then
        scoreHolder.addSoftConstraintMatch(kcontext, 10);
end

rule "maximizePinnedMatches"
    when
        PinnedMatch($systemId : systemId, $subscriptionId : subscriptionId)
        Match(systemId == $systemId, subscriptionId == $subscriptionId, confirmed == true) or
        (
            FreeMatch(systemId == $systemId, subscriptionId == $subscriptionId, $requiredMatchId : requiredMatchId) and
            Match(id == $requiredMatchId, confirmed == true)
        )
    then
        scoreHolder.addSoftConstraintMatch(kcontext, 1);
end

