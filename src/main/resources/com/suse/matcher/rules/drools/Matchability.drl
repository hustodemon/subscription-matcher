package com.suse.matcher.rules.drools;

import java.util.List;

import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.IncompatibleGroups;
import com.suse.matcher.facts.PartialMatch;
import com.suse.matcher.facts.Product;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.Subscription.Policy;
import com.suse.matcher.facts.SubscriptionProduct;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.InstalledProduct;

rule "generateHostedProduct"
when
	System($systemId : id, physical == false)
	InstalledProduct(systemId == $systemId, $productId : productId)
	HostGuest($hostId : hostId, guestId == $systemId)
then
	insertLogical(new HostedProduct($hostId, $productId));
end

rule "matchPhysicalOnlySubscription"
    when
        $system : System($systemId : id, physical == true)
        InstalledProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.PHYSICAL_ONLY,
            cpus >= $system.cpus,
            ignored == false
        )
    then
        int groupId = PartialMatch.generateGroupId($system.id, $productId, $subscription.id);
        insertLogical(new PartialMatch($system.id, $productId, $subscription.id, 100, groupId));
end

rule "matchInstanceSubscription"
    when
        $system : System($systemId : id)
        InstalledProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.INSTANCE,
            (cpus == null || cpus >= $system.cpus),
            ignored == false
        )
    then
        int groupId = PartialMatch.generateGroupId($system.id, $productId, $subscription.id);
        insertLogical(new PartialMatch($system.id, $productId, $subscription.id, 100, groupId));
end

rule "matchUnlimitedVirtualizationSubscription"
    when
        $system : System($systemId : id, physical == true, cpus != null)
        (
            InstalledProduct($productId : productId, systemId == $systemId) or
            (
                HostGuest(hostId == $systemId, $guestId : guestId) and
                InstalledProduct($productId : productId, systemId == $guestId)
            )
        )
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.UNLIMITED_VIRTUALIZATION,
            (cpus >= $system.cpus || stackable == true),
            ignored == false
        )
    then
        Integer consumedQuantity = ($system.cpus + $subscription.cpus - 1) / $subscription.cpus;
        int groupId = PartialMatch.generateGroupId($system.id, $productId, $subscription.id);
        insertLogical(new PartialMatch($system.id, $productId, $subscription.id, consumedQuantity * 100, groupId));
end

rule "matchFreeVirtualizationSubscription"
    when
      PartialMatch($hostId : systemId, $productId : productId, $subscriptionId : subscriptionId)
      Subscription(id == $subscriptionId, policy == Policy.UNLIMITED_VIRTUALIZATION, ignored == false)
      HostGuest(hostId == $hostId, $guestId : guestId)
      SystemProduct(systemId == $guestId, productId == $productId)
    then
      int groupId = PartialMatch.generateGroupId($hostId, $productId, $subscriptionId);
      insertLogical(new PartialMatch($guestId, $productId, $subscriptionId, 0, groupId));
end

rule "matchInheritedVirtualization"
    no-loop
    when
        // for each base product
        Product($baseProductId : id, base == true)

        // for each installed add-on product with an IV subscription
        Subscription($addonSubscriptionId : id, policy == Policy.INHERITED_VIRTUALIZATION, ignored == false)
        SubscriptionProduct($addonProductId : productId, subscriptionId == $addonSubscriptionId)
        (
            InstalledProduct($systemId : systemId, productId == $addonProductId) or
            (
                InstalledProduct($guestId : systemId, productId == $addonProductId) and
                HostGuest($systemId : hostId, guestId == $guestId)
            )
        )

        // for each match
        PartialMatch($baseSubscriptionId : subscriptionId, $cents : cents, $baseGroupId : groupId, productId == $baseProductId, systemId == $systemId)
    then
        int addonGroupId = PartialMatch.generateGroupId($baseGroupId, $addonSubscriptionId);
        insertLogical(new PartialMatch($systemId, $baseProductId, $baseSubscriptionId, $cents, addonGroupId));
        insertLogical(new PartialMatch($systemId, $addonProductId, $addonSubscriptionId, $cents, addonGroupId));
end

rule "match12Subscription"
    when
        // we want to explicitly bind 'physical' here as we want the rule to be reactivated on change of this attribute:
        $system : System($systemId : id, $physical : physical, cpus != null)
        InstalledProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.ONE_TWO,
            ignored == false
        )
        not (HostGuest(hostId == $systemId))
    then
        int groupId = PartialMatch.generateGroupId($systemId, $productId, $subscriptionId);
        if ($physical) {
            insertLogical(new PartialMatch($system.id, $productId, $subscription.id,
                ($system.cpus + $subscription.cpus - 1) / $subscription.cpus * 100,
                groupId));
        }
        else {
            insertLogical(new PartialMatch($system.id, $productId, $subscription.id, 50, groupId));
        }
end
