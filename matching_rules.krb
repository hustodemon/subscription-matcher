# rules to determine which system <-> subscription matches are possible
# ideally these should be readable by PM

physical_license_can_match
  foreach
    systems.physical($system)
    systems.arch($system, $arch)

    subscriptions.product($subscription, 'SLES')
    subscriptions.arch($subscription, $arch)
    subscriptions.type($subscription, physical)
  assert
    subscriptions.can_match($subscription, $system)
    python print "%s can match %s" % ($subscription, $system)

virtual_license_can_match
  foreach
    systems.arch($system, $arch)

    subscriptions.product($subscription, 'SLES')
    subscriptions.arch($subscription, $arch)
    subscriptions.type($subscription, virtual)
  assert
    subscriptions.can_match($subscription, $system)
    python print "%s can match %s" % ($subscription, $system)


# rules to do the actual assignment
# ideally these should be independent of subscription type

assign_subscriptions_to_host
  foreach
    subscriptions.type($subscription, virtual)
    subscriptions.can_match($subscription, $host)
    systems.is_guest_of($guest, $host)

    # make sure system does not have a subscription already
    notany
      subscriptions.assigned($_subscription, $host)

    # make sure subscription is not reused
    notany
      subscriptions.assigned($subscription, $_host)

  assert
    subscriptions.assigned($subscription, $host)
    python print "%s is assigned to %s" % ($subscription, $host)

assign_subscriptions_to_virt_system
  foreach
    subscriptions.type($subscription, virtual)
    subscriptions.can_match($subscription, $host)
    systems.is_guest_of($guest, $host)

    # make sure system does not have a subscription already
    notany
      subscriptions.assigned($_subscription, $guest)

  assert
    subscriptions.assigned($subscription, $guest)
    python print "%s is assigned free to %s" % ($subscription, $guest)


assign_subscriptions_to_systems
  foreach
    subscriptions.can_match($subscription, $system)

    # make sure system does not have a subscription already
    notany
      subscriptions.assigned($_subscription, $system)

    # make sure subscription is not reused
    notany
      subscriptions.assigned($subscription, $_system)
  assert
    subscriptions.assigned($subscription, $system)
    python print "%s is assigned to %s" % ($subscription, $system)


# rules to identify leftovers

list_free_subscriptions
  foreach
    subscriptions.id($subscription)
    notany
      subscriptions.assigned($subscription, $_system)
  assert
    python print "%s is free" % $subscription

list_non_compliant_systems
  foreach
    systems.id($system)
    notany
      subscriptions.assigned($_subscription, $system)
  assert
    python print "%s is not compliant" % $system
