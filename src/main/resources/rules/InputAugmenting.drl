package com.suse.matcher.rules;

import com.suse.matcher.model.Subscription;
import com.suse.matcher.model.System;

rule "ensure virtual systems are indeed virtual"
agenda-group "InputAugmenting"
when
    $guest : System(physical)
    $host : System(virtualSystemIds contains $guest.id)
then
    modify($guest) {
        physical = false;
    }
end

rule "map products of virtual systems to the host system"
agenda-group "InputAugmenting"
when
    $guest : System()
    $host : System(virtualSystemIds contains $guest.id)
    $pdid : Long() from $guest.productIds
then
    modify($host) {
        productIds.add($pdid)
    }
end
