package com.suse.matcher.rules;

import java.util.List;

import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.Match;
import com.suse.matcher.facts.Match.Kind;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.Subscription.Policy;
import com.suse.matcher.facts.SubscriptionProduct;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.SystemProduct;

rule "physical only subscription can match a system"
    agenda-group "Matchability"
    when
        $system : System($systemId : id, physical)
        SystemProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.PHYSICAL_ONLY,
            cpus >= $system.cpus
        )
    then
        insert(new Match($system.id, $productId, $subscription.id, null, Kind.POSSIBLE));
end

rule "unlimited virtualization subscription can match a system"
    agenda-group "Matchability"
    when
        $system : System($systemId : id, physical)
        (
            SystemProduct($productId : productId, systemId == $systemId) or
            (
                HostGuest(hostId == $systemId, $guestId : guestId) and
                SystemProduct($productId : productId, systemId == $guestId)
            )
        )
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.UNLIMITED_VIRTUALIZATION,
            (cpus >= $system.cpus || stackable)
        )
    then
        insert(new Match($system.id, $productId, $subscription.id, null, Kind.POSSIBLE));
end

rule "1-2 stackable subscription can match a system"
    agenda-group "Matchability"
    when
        $system : System($systemId : id)
        SystemProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(
            id == $subscriptionId,
            policy == Policy.TWO_TWO
        )
        not (HostGuest(hostId == $systemId))
    then
        insert(new Match($system.id, $productId, $subscription.id, null, Kind.POSSIBLE));
end
