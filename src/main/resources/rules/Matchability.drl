package com.suse.matcher.rules;

import java.util.List;

import com.suse.matcher.facts.Match;
import com.suse.matcher.facts.Match.Kind;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.SubscriptionProduct;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.SystemProduct;

rule "installed product matches subscription"
    agenda-group "Matchability"
    when
        $system : System($systemId : id)
        SystemProduct($productId : productId, systemId == $systemId)
        SubscriptionProduct($subscriptionId : subscriptionId, productId == $productId)
        $subscription : Subscription(id == $subscriptionId,
            (stackable == true || cpus >= $system.cpus),
            (
                (virtualizationPolicy == Subscription.PHYSICAL_ONLY && $system.physical) ||
                (virtualizationPolicy == Subscription.UNLIMITED_VIRTUALIZATION && $system.physical)
                // TWO_TWO can be applied to both physical and virtual systems
            )
        )
    then
        insert(new Match($system.id, $subscription.id, $productId, 1, Kind.POSSIBLE));
end
