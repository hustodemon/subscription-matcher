package com.suse.matcher.rules;

import com.suse.matcher.model.Match;
import com.suse.matcher.model.Match.Kind;
import com.suse.matcher.model.Subscription;
import com.suse.matcher.model.System;

rule "match valid user-specified matches"
    agenda-group "Matching"
    when
        Match($systemId : systemId, $subscriptionId : subscriptionId, kind == Kind.USER_PINNED) and
        Match(systemId == $systemId, subscriptionId == $subscriptionId, kind == Kind.POSSIBLE)
    then
        insert(new Match($systemId, $subscriptionId, Kind.CONFIRMED));
end

rule "match valid matches"
    agenda-group "Matching"
    when
        $system : System($systemId : id) and
        Match($subscriptionId : subscriptionId, systemId == $systemId, kind == Kind.POSSIBLE) and
        $subscription: Subscription($systemLimit : systemLimit, id == $subscriptionId, matchAnyProductOnSystem($system), systemLimit > 0) and
        not (Match(systemId == $systemId, subscriptionId == $subscriptionId, kind == Kind.CONFIRMED))
    then
        insert(new Match($systemId, $subscriptionId, Kind.CONFIRMED));
        modify($subscription){
            systemLimit = $systemLimit -1
        }
end
