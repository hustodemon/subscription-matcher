package com.suse.matcher.rules;

import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.Match;
import com.suse.matcher.facts.Match.Kind;
import com.suse.matcher.facts.Subscription;
import com.suse.matcher.facts.Subscription.Policy;
import com.suse.matcher.facts.System;

rule "flag invalid pinned matches"
    agenda-group "OutputCollection"
    when
        $pinned_match : Match($systemId : systemId, $productId : productId, $subscriptionId : subscriptionId, $quantity: quantity, kind == Kind.USER_PINNED)
        not (Match(systemId == $systemId, productId == $productId, subscriptionId == $subscriptionId, quantity == $quantity, kind == Kind.POSSIBLE))
    then
        insert(new Match($systemId, $productId, $subscriptionId, $quantity, Kind.INVALID));
end

rule "add matches for virtual machines included in hosts's subscriptions"
    agenda-group "OutputCollection"
    when
        $match : Match($hostId : systemId, $productId : productId, $subscriptionId : subscriptionId, kind == Kind.CONFIRMED)
        Subscription(id == $subscriptionId, policy == Policy.UNLIMITED_VIRTUALIZATION)
        HostGuest(hostId == $hostId, $guestId : guestId)
    then
        insert(new Match($guestId, $productId, $subscriptionId, 0.0, Kind.CONFIRMED));
end
