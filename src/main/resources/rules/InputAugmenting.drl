package com.suse.matcher.rules;

import com.suse.matcher.facts.HostGuest;
import com.suse.matcher.facts.System;
import com.suse.matcher.facts.SystemProduct;

rule "ensure virtual systems are indeed virtual"
agenda-group "InputAugmenting"
when
    HostGuest($hostId : hostId, $guestId : guestId)
    $guest : System(id == $guestId, physical)
then
    modify($guest) {
        physical = false;
    }
end

rule "map products of virtual systems to the host system"
agenda-group "InputAugmenting"
when
    HostGuest($hostId: hostId, $guestId : guestId)
    SystemProduct($productId : productId, systemId == $guestId)
then
    insert(new SystemProduct($hostId, $productId));
end
